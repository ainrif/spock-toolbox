import org.ajoberstar.grgit.operation.OpenOp

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

apply plugin: 'org.ajoberstar.grgit'
apply plugin: "com.jfrog.bintray"

ext.sharedManifest = manifest {
    attributes 'Implementation-Vendor': group,
            'Implementation-Title': name,
            'Implementation-Version': version
}

jar {
    manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest {
        from sharedManifest
    }
}

publishing {
    publications {
        toMaven(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    if (project.hasProperty('bintray_api_user') && project.hasProperty('bintray_api_key')) {
        user = bintray_api_user
        key = bintray_api_key
    }

    publications = ['toMaven']

    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        userOrg = 'ainrif'
        name = rootProject.name
        desc = 'Utils for Spock testing framework'
        websiteUrl = 'https://bitbucket.org/ainrif/spock-device'
        issueTrackerUrl = 'https://bitbucket.org/ainrif/spock-device/issues'
        vcsUrl = 'git@bitbucket.org:ainrif/spock-device.git'
        licenses = ['Apache-2.0']
        labels = ['test', 'spock', 'util', 'groovy']
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'Initial version'
            vcsTag = project.version
        }
    }
}

bintrayUpload {
    def git = new OpenOp(dir: '.').call()
    onlyIf {
        'master' == git.branch.current.name
    }
    doLast {
        if (bintray.dryRun) {
            logger.info("create tag `${project.version}` in git and push")
            return
        }

        git.tag.add(name: project.version, annotate: false, force: true)
        git.push(all: false)
        git.push(tags: true)
    }
}

publish.dependsOn bintrayUpload