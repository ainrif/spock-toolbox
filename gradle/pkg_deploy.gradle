apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.sharedManifest = manifest {
    attributes 'Implementation-Vendor': group,
            'Implementation-Title': name,
            'Implementation-Version': version
}

jar {
    manifest {
        from sharedManifest
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest {
        from sharedManifest
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenCentral(MavenPublication) {
            customizePom(pom)

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username findProperty('sonatypeUsername')
                password findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenCentral
}

void customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name project.name
            description 'Toolbox is a set of small utilities to make Spock Framework Specifications more readable and easier to write'

            url 'https://github.com/ainrif/spock-toolbox/'

            organization {
                name 'Ainrif'
                url 'https://github.com/ainrif'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/ainrif/spock-toolbox/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/ainrif/spock-toolbox'
                connection 'scm:git:https://github.com/ainrif/spock-toolbox.git'
                developerConnection 'scm:git:git@github.com:ainrif/spock-toolbox.git'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/ainrif/spock-toolbox/issues'
            }
            developers {
                developer {
                    name 'Andrew Malyhin'
                    email 'katoquro@gmail.com'
                }
            }
        }
    }
}