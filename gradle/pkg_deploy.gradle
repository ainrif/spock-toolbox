apply plugin: "com.jfrog.bintray"

ext.sharedManifest = manifest {
    attributes 'Implementation-Vendor': group,
            'Implementation-Title': name,
            'Implementation-Version': version
}

jar {
    manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest {
        from sharedManifest
    }
}

publishing {
    publications {
        toMaven(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = project.findProperty('bintray_api_user')
    key = project.findProperty('bintray_api_key')

    publications = ['toMaven']

    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        userOrg = 'ainrif'
        name = project.rootProject.name
        desc = 'Utils for Spock testing framework'
        websiteUrl = 'https://github.com/ainrif/spock-toolbox'
        issueTrackerUrl = 'https://github.com/ainrif/spock-toolbox/issues'
        vcsUrl = 'git@github.com:ainrif/spock-toolbox.git'
        licenses = ['Apache-2.0']
        labels = ['test', 'spock', 'util', 'groovy']
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

publish.dependsOn bintrayUpload